if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.greenrobot.greendao'
android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /**
     * 配置greendao3.1 配置数据库相关信息
     */
    greendao {
        schemaVersion 2 //数据库版本号
        daoPackage 'com.component.project.youlong.bean'
        targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao 包名
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    annotationProcessor deps.arouter_compiler
    implementation project(':componentmodules:module_common')
    implementation deps.butterknifes.butterknife
    annotationProcessor deps.butterknifes.butterknife_compiler
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.greenrobot:greendao-generator:3.2.2'
    //Android流式布局（FlowLayout）
    implementation 'com.hyman:flowlayout-lib:1.1.2'

    //没有使用特殊Header，可以不加这行
    implementation 'rouchuan.viewpagerlayoutmanager:viewpagerlayoutmanager:2.0.22'
    //蚯蚓导航 https://github.com/auv1107/tablayout-android
   // implementation 'com.github.hiliving:tablayout-android:1.0.1'
}
